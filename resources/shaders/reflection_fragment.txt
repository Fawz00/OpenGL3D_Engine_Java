#version 330 core

precision highp float;

uniform sampler2D TEXTURE_0;
uniform sampler2D TEXTURE_MER;
uniform sampler2D TEXTURE_DEPTH;
uniform sampler2D TEXTURE_NORMAL;

uniform mat4 ROTATION_MATRIX;
uniform mat4 PROJECTION_MATRIX;
uniform vec2 RESOLUTION;

in vec2 uv;

out vec4 FragColor;

bool rayIsOutofScreen(vec2 ray){
	return (ray.x > 1 || ray.y > 1 || ray.x < 0 || ray.y < 0) ? true : false;
}

vec4 TraceRay(vec3 rayPos, vec3 dir, int iterationCount){
	float sampleDepth;
	vec4 hitColor = vec4(0.);
	float rayDistance = 0.;
	vec3 startPos = rayPos;
	bool hit = false;

	for(int i = 0; i < iterationCount; i++){
		rayPos += dir;
		rayDistance = distance(startPos, rayPos);
		if(rayIsOutofScreen(rayPos.xy)){
			break;
		}

		sampleDepth = texture(TEXTURE_DEPTH, rayPos.xy).r;
		float depthDif = rayPos.z - sampleDepth;
		if(depthDif >= 0.0 && depthDif < 0.0001 && rayDistance > 0.0001){ //we have a hit
			hit = true;
			hitColor = texture(TEXTURE_0, rayPos.xy)*pow(texture(TEXTURE_MER, rayPos.xy).g+0.85, 8.0);
			hitColor.a = 1.0;
			break;
		}
	}
	return hitColor;
}

void main() {
	vec4 color = vec4(0.,0.,0.,1.);

	// SCREEN SPACE REFLECTION
	if(texture(TEXTURE_MER, uv).r >= 0.125) {
		float maxRayDistance = 50.0f;

		//View Space ray calculation
		vec3 pixelPositionTexture;
		pixelPositionTexture.xy = uv;
		vec4 normalView = texture(TEXTURE_NORMAL, pixelPositionTexture.xy);
		normalView.g = 1.-normalView.g;
		normalView.rgb = normalView.rgb*2.-1.;
		normalView = ROTATION_MATRIX*normalView;
		float pixelDepth = texture(TEXTURE_DEPTH, pixelPositionTexture.xy).r;
		pixelPositionTexture.z = pixelDepth;
		vec4 positionView = inverse(PROJECTION_MATRIX) * vec4(pixelPositionTexture * 2. - vec3(1.), 1.);
		positionView /= positionView.w;
		vec3 reflectionView = reflect(-positionView.xyz, normalize(normalView.xyz));
		if(reflectionView.z < 0){
			color = vec4(0.);
			return;
		}
		vec3 rayEndPositionView = positionView.xyz + reflectionView * maxRayDistance;


		//Texture Space ray calculation
		vec4 rayEndPositionTexture = PROJECTION_MATRIX * vec4(rayEndPositionView,1.);
		rayEndPositionTexture /= rayEndPositionTexture.w;
		rayEndPositionTexture.xyz = (rayEndPositionTexture.xyz + vec3(1.)) / 2.0f;
		vec3 rayDirectionTexture = rayEndPositionTexture.xyz - pixelPositionTexture;

		ivec2 screenSpaceStartPosition = ivec2(pixelPositionTexture.x * RESOLUTION.x, pixelPositionTexture.y * RESOLUTION.y); 
		ivec2 screenSpaceEndPosition = ivec2(rayEndPositionTexture.x * RESOLUTION.x, rayEndPositionTexture.y * RESOLUTION.y); 
		ivec2 screenSpaceDistance = screenSpaceEndPosition - screenSpaceStartPosition;
		int screenSpaceMaxDistance = max(abs(screenSpaceDistance.x), abs(screenSpaceDistance.y)) / 2;
		rayDirectionTexture /= max(screenSpaceMaxDistance, 0.001f);

		//trace the ray
		color = TraceRay(pixelPositionTexture, rayDirectionTexture, screenSpaceMaxDistance);
	}
	//color.rgb = texture(TEXTURE_DEPTH, uv.xy).rgb;

	FragColor = color;
}